archetype (adl_version=1.4; uid=907b790f-6452-3ad6-98f6-501749734040)
	openEHR-EHR-OBSERVATION.morse_fall.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2021-06-24">
		["name"] = <"Maryam Razavi">
		["organisation"] = <"Cambio CDS">
		["email"] = <"maryam.razavi@cambio.se">
	>
	lifecycle_state = <"AuthorDraft">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the results for each component parameter and the Morse fall score.">
			keywords = <"fall","risk","mental","status","prevention","gait">
			copyright = <"© Cambio CDS">
			use = <"Use to record the results for each component parameter and the Morse fall score.">
		>
	>
	other_details = <
		["references"] = <"Morse, Janice M., Robert M. Morse, and Suzanne J. Tylko. Development of a scale to identify the fall-prone patient. Canadian Journal on Aging/La Revue canadienne du vieillissement 8.4 (1989): 366-377.

O'Connell B, Myers H. The sensitivity and specificity of the Morse Fall Scale in an acute care setting. J Clin Nurs. 2002 Jan;11(1):134-6. doi: 10.1046/j.1365-2702.2002.00578.x. PMID: 11845750.">
		["current_contact"] = <"Maryam Razavi, Cambio CDS<maryam.razavi2009@gmail.com>">
		["MD5-CAM-1.0.1"] = <"D531EA5D3283D204C7FE11D3A02712EC">
		["build_uid"] = <"2c7ef31d-55e1-338b-8465-09e9ea88b5e2">
	>

definition
	OBSERVATION[at0000] matches {    -- Morse Fall Scale
		data matches {
			HISTORY[at0001] matches {    -- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- History of falling
										value matches {
											0|[local::at0005],
											25|[local::at0006]
										}
									}
									ELEMENT[at0007] occurrences matches {0..1} matches {    -- Secondary diagnosis
										value matches {
											0|[local::at0005],
											15|[local::at0006]
										}
									}
									ELEMENT[at0010] occurrences matches {0..1} matches {    -- Ambulatory aid
										value matches {
											0|[local::at0011],
											15|[local::at0012],
											30|[local::at0013]
										}
									}
									ELEMENT[at0014] occurrences matches {0..1} matches {    -- Intravenous therapy or heparin lock
										value matches {
											0|[local::at0006],
											20|[local::at0005]
										}
									}
									ELEMENT[at0017] occurrences matches {0..1} matches {    -- Gait
										value matches {
											0|[local::at0018],
											10|[local::at0019],
											20|[local::at0020]
										}
									}
									ELEMENT[at0021] occurrences matches {0..1} matches {    -- Mental status
										value matches {
											0|[local::at0022],
											15|[local::at0023]
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {    -- Total score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..125|}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0025] matches {    -- Tree
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0026] occurrences matches {0..*} matches {    -- Extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Morse Fall Scale">
					description = <"An assessment used to predict risk of falling based on personal fall history, mental status and other risk factors.">
					comment = <"Original version, c 1989.">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"Default, unspecified point in time or interval event which may be explicitly defined in a template or at run-time.">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"History of falling">
					description = <"Immediate or previous. This item looks at whether the patient had an episode of falling during current stay or has a history of falls, regardless of their cause (for example, gait type or seizures).">
				>
				["at0005"] = <
					text = <"No">
					description = <"">
				>
				["at0006"] = <
					text = <"Yes">
					description = <"">
				>
				["at0007"] = <
					text = <"Secondary diagnosis">
					description = <"2 or more medical diagnoses in chart.">
				>
				["at0008"] = <
					text = <"Yes">
					description = <"">
				>
				["at0009"] = <
					text = <"No">
					description = <"">
				>
				["at0010"] = <
					text = <"Ambulatory aid">
					description = <"Refers to the patient making use of walking aid (cane, crutches or wheelchair).">
				>
				["at0011"] = <
					text = <"None/ bed rest/ nurse assist">
					description = <"">
				>
				["at0012"] = <
					text = <"Crutches/ cane/ walker">
					description = <"">
				>
				["at0013"] = <
					text = <"Furniture">
					description = <"">
				>
				["at0014"] = <
					text = <"Intravenous therapy or heparin lock">
					description = <"">
				>
				["at0016"] = <
					text = <"No">
					description = <"">
				>
				["at0017"] = <
					text = <"Gait">
					description = <"This item valuates the patient’s balance status.">
				>
				["at0018"] = <
					text = <"Normal/ bed rest/ wheelchair">
					description = <"">
				>
				["at0019"] = <
					text = <"Weak">
					description = <"Weak gait means that the patient is in a stooped state but able to lift head, seeking support from furniture for reassurance, while walking with small steps.">
				>
				["at0020"] = <
					text = <"Impaired">
					description = <"Impaired gait is when the patient walks with short steps and his or her head down. The patient rises from chair with difficulty, walking aid may be required and impaired balance is noticed.">
				>
				["at0021"] = <
					text = <"Mental status">
					description = <"Assessed through the consistency of the patient’s answers.">
				>
				["at0022"] = <
					text = <"Oriented to own ability">
					description = <"">
				>
				["at0023"] = <
					text = <"Overestimates/ forgets limitations">
					description = <"">
				>
				["at0024"] = <
					text = <"Total score">
					description = <"The sum of the ordinal scores recorded for each of the six component responses.">
				>
				["at0025"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0026"] = <
					text = <"Extension">
					description = <"Additional information required to extend the model with local content or to align with other reference models or formalisms.">
					comment = <"For example: local information requirements; or additional metadata to align with FHIR.">
				>
			>
		>
	>
